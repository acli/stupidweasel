#!/usr/bin/perl
# vi:set sw=2 ai sm:
#
=encoding UTF-8

=head1 Name

slgw – sysline gateway
=cut
# Copyright ©2020 by Ambrose Li <ambrose.li@gmail.com>

use strict;
use integer;

# see https://stackoverflow.com/questions/6162484/why-does-modern-perl-avoid-utf-8-by-default for these crazy hacks
# THERE ARE MORE CRAZY HACKS THERE FOR MORE COMPLEX PROJECTS!!
#use v5.14; # //u modifier
use utf8;
use open qw( :encoding(UTF-8) :std );
use charnames qw( :full :short );
use feature "unicode_strings";

use POSIX;
use Getopt::Long qw(:config no_auto_abbrev bundling no_ignore_case);

use Data::Dumper;

use vars qw( $debug_p );
use vars qw( $dry_run_p );
use vars qw( $test_p );
use vars qw( $n );
use vars qw( $output_path );

use vars qw( $prefer_cr_hack_p );
$prefer_cr_hack_p = 1;

GetOptions(
  'debug|d' => \$debug_p,
  'dry-run|n' => \$dry_run_p,
  'test' => \$test_p,
) || exit(1);

$output_path = $test_p? '/tmp/slgw.out': "/var/mail/$ENV{USER}";

open(OUTPUT, '>>', $output_path) || die "$output_path: $!\n";
my($headers, $from);
my($encoding, $qp_p);
my($type, $multipart_p);
for (;;) {
  my $s = scalar <>;
last unless defined $s;
  $headers .= $s;
last if $s eq "\n";
  if ($s =~ /^From:.*<([^<>\s]*)>/si) {
    $from = $1;
  } elsif ($s =~ /^From:\s*(\S+)\s*$/si) {
    $from = $1;
  } elsif ($s =~ /^From:\s*(.*)\s*$/si) {
    $from = $1;
    $from =~ s/\([^\(\)]*?\)//sg;
    $from =~ s/^\s+//s;
    $from =~ s/\s+$//s;
    $from = $1 if $from =~ /\s/ && $from =~ /([^\s\@]+\@[^\s\@]+)/; # XXX
  } elsif ($s =~ /^Content-Type:\s*([^\s;]+)/is) {
    $type = lc $1;
    $multipart_p = $type =~ /^multipart\//;
    print STDERR "Content-Type detected, type is $type\n" if $debug_p;
  } elsif ($s =~ /^Content-Transfer-Encoding:\s*([^\s;]+)/is) {
    $encoding = lc $1;
    $qp_p = $encoding eq 'quoted-printable';
    print STDERR "Content-Transfer-Encoding detected, encoding is $encoding\n" if $debug_p;
  }
  $n += 1;
}
if ($n) {
  $from = 'MAILER-DAEMON' unless defined $from && $from =~ /\@/ && $from !~ /\s/;
  print STDERR "from=($from)\n" if $debug_p;
  print OUTPUT sprintf("From %s %s\n", $from, scalar gmtime) unless $dry_run_p;
  print OUTPUT $headers unless $dry_run_p;
  for (;;) {
    my $s = scalar <>;
  last unless defined $s;
    $s = "$1$2" if $s =~ /^(.*?)\r(\n)$/s; # apparently Evolution adds \r to literally every single line, but only in the body
    if ($s =~ /^From /s) {
      print STDERR "Bare \"From \" in message body, trying to work around\n" if $debug_p;
      if ($qp_p) {
	$s =~ s/^F/=46/s;
      } elsif ($multipart_p) { # do nothing if multipart, on the assumption that mail cannot terminate before the final MIME boundary
	;
      } elsif ($prefer_cr_hack_p) {
	$s = "$1\r$2" if $s =~ /^(.*?[^\r])(\n)$/s; # re-insert \r on the assumption headers cannot end in \r\n
      } else {
	$s =~ s/^/>/s;
      }
    }
    print OUTPUT $s unless $dry_run_p;
    $n += 1;
  }
}
print OUTPUT "\n" if $n;	# doesn't matter for sysline; needed to keep the file actual mbox format
print "$n line(s) read\n" if $debug_p;

__END__

=head1 Synopsis

B<slgw> [B<-d>] [B<-n>] [B<-t>]

=head1 Description

F<slgw> uses Evolution’s filter feature to simulate mail delivery to a local mailbox,
so that L<sysline(1)>’s mail notification features can function on a system with no functional L<sendmail(8)>.

F<slgw> reads a maildir-format mail (the format used by Evolution) from standard input
and appends an equivalent mbox-format mail to an output file,
normally /var/mail/I<$USER>,
the per-user local mailbox.

Because the mbox format has trouble with lines starting with “From ”,
F<slgw> tries to compensate either by changing the F to =46
or inserting a > sign before the F.
The latter workaround is not done if the mail it receives
is a multipart mail.

=head1 Options

=over

=item -d, --debug

Debug mode. Displays debugging messages on standard error.

=item -n, --dry-run

Dry-run mode. Do not actually append anything to the output file.

=item -t, --test

Test mode. Changes output file to /tmp/slgw.out.

=back

=head1 See also

L<stupidweasel(1)>,
L<sysline(1)>

=head1 Reporting bugs

Please report bugs through L<Github|https://github.com/acli/stupidweasel/issues>.
=cut
